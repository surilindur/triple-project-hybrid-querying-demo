# Combined query across: Solid pod, IDSM, ChEDBI

PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX lscr: <http://purl.org/lscr#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX vocab: <http://rdf.ncbi.nlm.nih.gov/pubchem/vocabulary#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rh: <http://rdf.rhea-db.org/>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
PREFIX thd: <urn:triple-hybrid-demo:>

SELECT ?protein2 ?OMA_LINK ?taxon_sci_name WHERE {
  # Solid
  ?SOLIDPOD thd:predicate ?CAS .

  # IDSM
  ?SYNONYM a sio:CHEMINF_000446 . # CAS registry number
  ?SYNONYM sio:SIO_000300 ?CAS .
  ?SYNONYM sio:SIO_000011 ?COMPOUND .

  ?COMPOUND a vocab:Compound .

  ?ATTRIBUTE a sio:SIO_011120 . # molecular structure file
  ?ATTRIBUTE sio:SIO_000011 ?COMPOUND .
  ?ATTRIBUTE sio:SIO_000300 ?MOLFILE .

  # IDSM / ChEBI
  SERVICE <https://idsm.elixir-czech.cz/sparql/endpoint/chebi> {
    [
      sachem:compound ?SIMILAR_COMPOUND ;
        sachem:score ?SCORE
    ] sachem:similaritySearch [
      sachem:query ?MOLFILE ;
      sachem:cutoff "0.7"^^xsd:double ;
      sachem:similarityRadius "3"^^xsd:integer ;
      sachem:aromaticityMode sachem:aromaticityDetectIfMissing ;
      sachem:tautomerMode sachem:inchiTautomers
    ] .
  }

  # Rhea
  SERVICE <https://sparql.rhea-db.org/sparql> {
    ?rhea rdfs:subClassOf rh:Reaction .
    ?rhea rh:equation ?equation .
    ?rhea rh:accession ?accession .
    ?rhea rh:side/rh:contains/rh:compound/(rh:chebi|(rh:reactivePart/rh:chebi)|(rh:underlyingChebi/rh:chebi)) ?SIMILAR_COMPOUND .
  }

  # Uniprot
  SERVICE <https://sparql.uniprot.org/sparql> {
    # GRAPH <http://sparql.uniprot.org/uniprot> {
    ?uniprot up:reviewed true .
    ?uniprot up:mnemonic ?mnemo .
    ?uniprot up:organism ?taxid .
    ?uniprot up:annotation/up:catalyticActivity/up:catalyzedReaction ?rhea . # where ?rhea comes from query upwards
    # }
  }

  # OMA
  SERVICE <https://sparql.omabrowser.org/sparql> {
    #The three that contains Orthologs. The leafs are proteins.
    #This graph pattern defines the relationship protein1 is Orthologs to protein2
    ?cluster a orth:OrthologsCluster .
    ?cluster orth:hasHomologousMember ?node1.
    ?cluster orth:hasHomologousMember ?node2.
    ?node2 orth:hasHomologousMember* ?protein2.
    ?node1 orth:hasHomologousMember* ?protein1.

    #Specify the protein to look for its orthologs
    ?protein1 lscr:xrefUniprot ?uniprot .

    #The OMA link to the second protein
    ?protein2 rdfs:seeAlso ?OMA_LINK .
    ?protein2 orth:organism/obo:RO_0002162/up:scientificName ?taxon_sci_name .

    FILTER(?node1 != ?node2)
  }
} ORDER BY DESC(?SCORE)
