# Combined query across: Solid pod, IDSM

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX vocab: <http://rdf.ncbi.nlm.nih.gov/pubchem/vocabulary#>
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
PREFIX thd: <urn:triple-hybrid-demo:>

SELECT ?CAS ?COMPOUND ?SIMILAR_COMPOUND ?SCORE WHERE {
  ?SOLIDPOD thd:predicate ?CAS .

  ?SYNONYM a sio:CHEMINF_000446 . # CAS registry number
  ?SYNONYM sio:SIO_000300 ?CAS .
  ?SYNONYM sio:SIO_000011 ?COMPOUND .

  ?COMPOUND a vocab:Compound .

  ?ATTRIBUTE a sio:SIO_011120 . # molecular structure file
  ?ATTRIBUTE sio:SIO_000011 ?COMPOUND .
  ?ATTRIBUTE sio:SIO_000300 ?MOLFILE .

  SERVICE <https://idsm.elixir-czech.cz/sparql/endpoint/chebi> {
    [
      sachem:compound ?SIMILAR_COMPOUND ;
        sachem:score ?SCORE
    ] sachem:similaritySearch [
      sachem:query ?MOLFILE ;
      sachem:cutoff "0.7"^^xsd:double ;
      sachem:similarityRadius "3"^^xsd:integer ;
      sachem:aromaticityMode sachem:aromaticityDetectIfMissing ;
      sachem:tautomerMode sachem:inchiTautomers
    ] .
  }
} LIMIT 1
# ORDER BY DESC(?SCORE) 
