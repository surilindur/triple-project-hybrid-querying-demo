# https://sparql.omabrowser.org/sparql

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX lscr: <http://purl.org/lscr#>

SELECT ?protein2 ?OMA_LINK ?taxon_sci_name WHERE {
  VALUES ?Uniprot_link { <http://purl.uniprot.org/uniprot/P72156> } # here

  #The three that contains Orthologs. The leafs are proteins.
  #This graph pattern defines the relationship protein1 is Orthologs to protein2
  ?cluster a orth:OrthologsCluster.
  ?cluster orth:hasHomologousMember ?node1.
  ?cluster orth:hasHomologousMember ?node2.
  ?node2 orth:hasHomologousMember* ?protein2.
  ?node1 orth:hasHomologousMember* ?protein1.

  #Specify the protein to look for its orthologs
  ?protein1 lscr:xrefUniprot ?Uniprot_link.

  #The OMA link to the second protein
  ?protein2 rdfs:seeAlso ?OMA_LINK.
  ?protein2 orth:organism/obo:RO_0002162/up:scientificName ?taxon_sci_name.

  FILTER(?node1 != ?node2)
}
